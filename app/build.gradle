apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.asksven.betterbatterystats"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 85
        versionName "2.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    // we have different package name for the variants
    flavorDimensions "edition"
    productFlavors {
        gplay {
            dimension "edition"
            applicationId "com.asksven.betterbatterystats"
        }

        xdaedition {
            dimension "edition"
            applicationId "com.asksven.betterbatterystats_xdaedition"
        }

    }
    // and a naming policy for the apks
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def project = "betterbatterystats"
            def SEP = "_"
            def flavor = variant.productFlavors[0].name
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionName
            def date = new Date();
            def formattedDate = date.format('yyyy-MM-dd_HHmmss')

            //def newApkName = project + SEP + flavor + SEP + buildType + SEP + version + SEP + formattedDate + ".apk"
            def newApkName = project + SEP + flavor + SEP + buildType + SEP + version + ".apk"
            output.outputFile = new File(output.outputFile.parent, newApkName)
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
    // @TODO: remove when fixed
    // bug introduced by android studio 2.3 -> force all support libs to this version
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '25.2.0'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'com.android.support:design:25.3.1'
    compile 'com.twofortyfouram:android-plugin-client-sdk-for-locale:[4.0.2, 5.0['
    // for local tests (app/src/test)
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    // Required for instrumented tests (app/src/androidTest)
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4-rule-agent:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4-rule:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    compile files('libs/androidcommon.jar')
    compile files('libs/RootTools-3.4.jar')
    // Firebase analytics
    compile 'com.google.firebase:firebase-core:10.2.1'
}

apply plugin: 'com.google.gms.google-services'
